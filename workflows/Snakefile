import yaml

from misolib.common import merge_yaml

configfile: "config.yaml"

rule misosoup:
    input:
        config["base_medium"]
    output:
        "out/cache/{strain}/{carbon_source}.yaml"
    params:
        models=config["models"]
    shell:
        "misosoup {params.models} "
        "--base-medium {input} "
        "--carbon-sources {wildcards.carbon_source} "
        "--output {output} "
        "--strain {wildcards.strain} "
        "--parsimony "


rule gather:
    input:
        [
            f"out/cache/{strain}/{carbon_source}.yaml"
            for carbon_source in config["carbon_sources"]
            for strain in config["strains"]
        ],
    output:
        "out/communities.yaml",
    resources:
        mem_mb=8192,
    run:
        solutions = merge_yaml(input)
        output_dump = yaml.dump(solutions)
        with open(output[0], "w") as fd:
            fd.write(output_dump)
